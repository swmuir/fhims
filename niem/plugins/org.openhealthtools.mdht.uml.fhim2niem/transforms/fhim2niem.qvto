modeltype uml uses 'http://www.eclipse.org/uml2/2.1.0/UML';
modeltype ecore "strict" uses ecore('http://www.eclipse.org/emf/2002/Ecore');
modeltype term uses 'http://www.openhealthtools.org/mdht/schemas/term/1';


transformation fhim2niem(in fhimModel : uml,in niemCoreModel : uml, out niemModel : uml );

main() {
	
	log('Transforming Federal Health Information Model (PIM) to National Information Exchange Model (PSM)');
	
	// Currently hard coded vocabulary import - need to add dynamic discovery support
	var vocabularyPackage :=  fhimModel.rootObjects()[uml::Model]->getPackageImport('HITSP-C80')->asSequence()->first();

	// Create NIEM PSM
	fhimModel.rootObjects()[uml::Package]-> map toNIEM();
	
	

	
}



mapping uml::Package::toNIEM() : uml::Package
{
	// Clone the FHIM instance to NIEM instance
	init{
		result := self.deepclone().oclAsType(Package);
		
		//result.ownedType.destroy();
	
	}
	

	// Update the generalizations of the extension 
	result.allSubobjects()[uml::Generalization].map mapGeneralizations(); 
	
	// Update and bind to terminology model 
	 result.allSubobjects()[uml::Class] -> map mapTerminology();
	 

	// update the name - do this last
	result.name := self.name;
	
	
	var constraintPackage : uml::Package := object uml::Package  {
		name := 'Constraints';
	}	;
	
	constraintPackage.ownedType += result.ownedType;
	
	result.packagedElement += constraintPackage;
	
   var exchangePackage : uml::Package := object uml::Package  {
		name := 'Exchange';
	};
	
	var exchangeClass : uml::Class := object uml::Class
	{
		name := 'Exchange'
	};
	
	exchangePackage.ownedType += exchangeClass;
	
	exchangeClass.ownedAttribute += constraintPackage.ownedType[uml::Class]->map mapConstraintsToExchange();
	
	
	result.packagedElement += exchangePackage;
	
	 //update to niem naming convention
	constraintPackage.ownedType[uml::Class]-> map mapNiemNaming();
	
	
}


// map generalizations to niem core
mapping  inout uml::Generalization::mapGeneralizations() 
{
	log('Maping FHIM generalizations');	
	// use cda template profile for the meantime - need to create niem version
	var cdastereotype : uml::Stereotype := self.general.getAppliedStereotype('CDA::CDATemplate');
	
	var templateId :=	self.general.getValue(cdastereotype,"templateId");
	
	self.general := getFHIMClass(templateId.repr());

}

mapping inout uml::Class::mapTerminology() {
	
	log ('Mapping FHIM terminology ');
	
	nestedClassifier += self.ownedAttribute -> map mapTerminology();
	
	// Appying xsd after the add so the profile is available
	nestedClassifier[uml::Class]->map applyXSD(); 
	
}


mapping inout uml::Class::mapNiemNaming() {
	
	log ('Mapping FHIM Naming ');
	
	name := name +'Type';
	
	
}

mapping  uml::Class::mapConstraintsToExchange() : uml::Property {

	init {
		result := object uml::Property {
			name := self.name;
			type := self;
		};	
	}
	
	
}

mapping inout uml::Property::mapTerminology() : uml::Class when { self.type != null and  self.type.name = 'Code'  }
{	

	// create the class restul
	init {
		result := object uml::Class {
		name := '_'+self.name;
		general := getNiemClass('anyType');
		};	
	
		// value property 
		var valueProperty : uml::Property := 
		object uml::Property
		{
			name := 'value';
			//type := getNiemDataType('string');
		};
	
		// value set id attribute
		var valueSetIdProperty : uml::Property := 
		object uml::Property
		{
			name := 'valueSetId';
		};
	
		// get the corresponding fhim property
		var fhimProperty : uml::Property := getFHIMProperty(self.qualifiedName);

		var termstereotype : uml::Stereotype := fhimProperty.getAppliedStereotype('Terminology::ValueSetConstraint');
	
		var enumerationSource : uml::Enumeration := getTerminologyEnumeration(fhimProperty.getValue(termstereotype,"reference").repr());
	
		var enumerationTarget : uml::Enumeration  := enumerationSource.deepclone().oclAsType(uml::Enumeration );
	
		var termstereotype2 : uml::Stereotype := enumerationSource.getAppliedStereotype('Terminology::ValueSetVersion');

		valueSetIdProperty.defaultValue := object uml::LiteralString {
			value  := enumerationSource.getValue(termstereotype2,"identifier")->oclAsType(String)->asSequence()->first();
		};

		result.ownedAttribute += valueProperty;

		result.ownedAttribute += valueSetIdProperty ;
	
		result.nestedClassifier += enumerationTarget ;
		
		// set the value to the inlined enumeration
		valueProperty.type := enumerationTarget;
		
	}

	// set the type of the property
	self.type := result;

}


mapping  inout uml::Class::createInnerClasses() 
{
	nestedClassifier += self.ownedAttribute -> map createInnerClasses() ;

}


// apply xsd to valuesetid 
mapping  inout uml::Property::applyXSD() 
{
	
	if (name = 'valueSetId') then
	{
		var xsdstereotype : uml::Stereotype := getApplicableStereotype('XMLSchema::Attribute');

		applyStereotype(xsdstereotype);
		
	}endif;
	 
}


mapping  inout uml::Class::applyXSD() 
{
	self.ownedAttribute[uml::Property] ->map applyXSD();
}

mapping inout uml::Property::createInnerClasses() : uml::Class
{
	
	init {
		result := object uml::Class {
			name := '_' + self.name;
			general := getNiemClass('anyType');
		};	
	}

	
	var p : uml::Property := 
	object uml::Property
	{
		name := 'valueSetId';
	};

	result.ownedAttribute += p;

	self.type = result;


}


query getNiemClass(in niemClassName: String ) : uml::Class
{   
   return niemCoreModel.objectsOfType(uml::Class)->asSequence()->select(niemClassName=name)->first();
}

query getNiemDataType(in niemClassName: String ) : uml::DataType
{   
   return niemCoreModel.objectsOfType(uml::DataType)->asSequence()->select(niemClassName=name)->first();
}

query getFHIMProperty(in fhimPropertyName: String ) : uml::Property
{   
   return fhimModel.objectsOfType(uml::Property)->asSequence()->select(fhimPropertyName=qualifiedName)->first();
}



query getFHIMClass(in fhimClassName: String ) : uml::Class
{   
   return niemCoreModel.objectsOfType(uml::Class)->asSequence()->select(fhimClassName=name)->first();
}


query getTerminologyEnumeration(in termClassName: String ) : uml::Enumeration
{

	var vocabularyPackage :=  fhimModel.rootObjects()[uml::Package].getImportedPackages()->select(name='HITSP-C80')->asSequence()->first();   
	 
  var enumeration := vocabularyPackage.allSubobjects()[uml::Enumeration]->asSequence()->select(termClassName=qualifiedName)->first();

  return enumeration;
  
}

query uml::Model::getPackageImport(in packageName: String )  : uml::Package
{
	self.packageImport->displayPackage();

	 return self.packageImport->select(packageName=importedPackage.name)->asSequence()->first().importedPackage;
}

// Add to general purpose qvt library  
 
mapping uml::PackageImport::displayPackage() 
{
	log('uml::Package is ' +self.importedPackage.qualifiedName);
}

mapping uml::Stereotype::displayStereotype()
{
	log('uml::Stereotype is ' + self.qualifiedName);	
}
mapping uml::NamedElement::displayNamedElement()
{
	log('uml::NameElement is ' + self.qualifiedName);	
}
 
 

